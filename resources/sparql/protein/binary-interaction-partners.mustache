PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX franzOption_clauseReorderer: <franz:identity>

# Return the proteins known to participate in binary interactions with the input protein identifier.

select (<{{bio-id}}> as ?source_bio_id) (<{{ice-id}}> as ?ice_id) ("{{src-id}}" as ?ext_source_id) ?interaction_label ?partner_bio_id
{
  ?specificbioentity rdfs:subClassOf <{{bio-id}}> .

  ?has_participant_r1 owl:someValuesFrom ?specificbioentity .
  ?has_participant_r1 owl:onProperty obo:RO_0000057 . # RO:has_participant
  ?interaction rdfs:subClassOf ?has_participant_r1 .

  # confirm has_participant cardinality
  ?interaction rdfs:subClassOf ?rcard .
  ?rcard owl:cardinality 2 . # require 2 participants for a binary interaction
  ?rcard owl:onProperty obo:RO_0000057 . # RO:has_participant

  ?interaction rdfs:subClassOf* obo:MI_0190 . # MI:interaction_type
  ?interaction rdfs:label ?interaction_label .

  ?interaction rdfs:subClassOf ?has_participant_r2 .
  ?has_participant_r2 owl:onProperty obo:RO_0000057 . # RO:has_participant
  ?has_participant_r2 owl:someValuesFrom ?interacting_partner .
  ?interacting_partner rdfs:subClassOf ?partner_bio_id .
  filter (?partner_bio_id != <{{bio-id}}>) .
}

# Local Variables:
# mode: sparql
# End:
